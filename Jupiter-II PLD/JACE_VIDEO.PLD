Name     JII_VIDEO ;
PartNo   00 ;
Date     02/26/2020 ;
Revision 01 ;
Designer Ricardo Fernandes Lopes ;
Company   ;
Assembly None ;
Location  ;
Device   g22v10 ;

/* *************** INPUT PINS ******************** */
PIN  1 = INV_CLK ; /* D flip-flop clock input (connect to INVCLK) */
PIN  2 = PIXEL   ; /* Pixel from Video Shift Register */
PIN  3 = INVCHR  ; /* Inverse Character Flag from Screen RAM */
PIN  4 = !SCRADR ; /* Screen RAM Addressing */
PIN  5 = RGTPIX  ; /* Character Right Pixel */
PIN  6 = VIDEN   ; /* Video Frame Active */
PIN  7 = !VRAM   ; /* Z80 CPU Video RAM Addressing */
PIN  8 = !WR     ; /* Z80 CPU Write Enable */
PIN  9 = A10     ; /* Z80 CPU Address line */
PIN 10 = A11     ; /* Z80 CPU Address line */
PIN 11 = OSC     ; /* Main Clock */
                   /* pin 12 = GND */
/* PIN 13 = unused */

/* *************** OUTPUT PINS ******************** */
                   /* pin 24 = VCC */
PIN 23 = INVCLK  ; /* Background Load: feeds BLGCLK input */
PIN 22 = INV     ; /* Character Background */
PIN 21 = LUMA	 ; /* Video Luminance */
PIN 20 = !SCRRAM ; /* Screen RAM Enable */
PIN 19 = !CHRRAM ; /* Char Set RAM Enable */
PIN 18 = !CHRADR ; /* Char Set RAM CPU Addressing */
PIN 17 = !VRAMWR ; /* Video RAM Write Enable */
PIN 16 = !VLOAD  ; /* Video Shift Register Load */
PIN 15 = !VHOLD  ; /* Video Priority mechanism */
PIN 14 = VCLK    ; /* Video Clock */

/* _______________ EQUATIONS _____________________ */
VCLK = !OSC ;                 /* Video Clock */
CHRRAM = !(SCRADR & !A11) ;   /* Char Set RAM Enable */
SCRRAM = !(SCRADR & CHRRAM) ; /* Screen RAM Enable */
CHRADR = !SCRRAM ;            /* Char Set RAM CPU Addressing */
VRAMWR = SCRADR & WR ;        /* Char and Screen RAM Write Enable */
VLOAD  = VIDEN & RGTPIX ;     /* Load Video Shift Register */
VHOLD  = VIDEN & A10 ;        /* Video memory busy */
LUMA   = PIXEL $ INV ;        /* Video Luminance (Video Pixel On) */
INVCLK = RGTPIX & VCLK ;      /* Inverse character latch */
/* Inverse character latch using a D type Flip-Flop
                         /* Clock   = INVCLK (externally wired) */
INV.D = VIDEN & INVCHR ; /* Data    = Inverse Character flag */
INV.AR = 'b'0 ;          /* Reset   = Reset not active */
INV.SP = 'b'0 ;          /* Preseet = Preset not active */


