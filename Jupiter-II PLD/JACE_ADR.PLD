Name     JII_ADR ;
PartNo   00 ;
Date     02/26/2020 ;
Revision 01 ;
Designer Ricardo F. Lopes ;
Company   ;
Assembly None ;
Location  ;
Device   g22v10 ;

/* *************** INPUT PINS ******************** */
PIN  1 = MIC_CLK ; /* D-FlipFlop Clk: connect to MICCLK */
PIN  2 = H0 ;      /* 3.276 MHz */
PIN  3 = !VHOLD ;  /* Video Hold when Video Circuit Priority */
PIN  4 = !RD ;     /* CPU Read Enable */
PIN  5 = !WR ;     /* CPU Write Enable */
PIN  6 = D3 ;      /* CPU Data Bus: D3 */
PIN  7 = A0 ;      /* CPU Address Line A0 */
PIN [8..11] = [A12..15] ; /* CPU Address Address lines */
                   /* PIN 12 = GND */
PIN 13 = !IORQ ;   /* CPU I/O Request */
PIN 14 = !MREQ ;   /* CPU Memory Request */


/* *************** OUTPUT PINS ******************** */
                  /* pin 24 = VCC */
PIN 23 = MICCLK ; /* MIC.CLK output: connect to MIC_CLK input */
PIN 22 = !VRAM ;  /* Video RAM Access */
PIN 21 = !RAM ;   /* RAM Enable */
PIN 20 = !ROM ;   /* ROM Enable */
PIN 19 = !WAIT ;  /* CPU Wait */
PIN 18 = !CLK ;   /* CPU Clock */
PIN 17 = SPKR ;   /* Speaker Out (RS-FlipFlop out) */
PIN 16 = MIC ;    /* Tape Mic Out (D-FlipFlop out) */
PIN 15 = !KBDRD ; /* Keyboard Read Enable */


/* _______________ DEFINITIONS __________________ */
FIELD ADDRS = [A15..12] ; /* Z80 CPU Address lines */

/* _______________ EQUATIONS ___________________ */
/* Memory Addressing */
ROM  = MREQ & ADDRS:[0000..1FFF] ; /* 8K ROM Addressing */
VRAM = MREQ & ADDRS:[2000..2FFF] ; /* 4K Video RAM Addressing */
RAM  = MREQ & ADDRS:[3000..3FFF] ; /* 4K RAM Addressing */

/* I/O Addressing */
KBDRD  = IORQ & RD & !A0 ; /* Read Keyboard */
MICCLK = IORQ & WR & !A0 ; /* Latch MIC Data bit */ 

/* CPU signals */
WAIT = VRAM & VHOLD ; /* CPU Wait: Video circuit priority */
CLK  = H0 ;           /* CPU Clock */

/* Speaker Output using a SR-type flip flop: Q = !Q & S # Q & !R */
SPKR = !SPKR & MICCLK # SPKR & !KBDRD ;

/* Tape MIC output latch using a D-type flip flop */
                 /* Clock  = MICCLK (externally wired) */
MIC.D  = D3 ;    /* Data   = CPU D3 data line */
MIC.AR = 'b'0 ;  /* Reset  = not active */
MIC.SP = 'b'0 ;  /* Preset = not active */
